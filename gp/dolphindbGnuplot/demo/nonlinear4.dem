#
# Nonlinear color axis
# Confirm equivalence of "set log cb" and 
# "set nonlinear cb via log10(z) inv 10**z"
#

set style function pm3d
set palette cubehelix negative
unset key
set title offset 0, -3
set view 65, 34, 1.00627, 1.41667
unset border
unset xtics; unset ytics
set cbtics add (450) offset -0.5,0
unset mcbtics

set isosamples 20, 20
set xyplane at 0
set xrange [  -2.00 : 6.00 ]
set yrange [  -5.00 : 5.00 ]
set zrange [  -50.0 : 450. ]
set cbrange [ 0.100 : 450. ]

set multiplot layout 1,3

# Linear colors
#
set title "Linear cb axis"
set ztics
set border 16
splot 5 + 100.*sin(sqrt(x**2+y**2))/(x**2+y**2)

# Log-scale colors
#
set title "set log cb"
set logscale cb 10
unset ztics
unset border
replot

# Nonlinear color axis (log scale)
#
set title "set nonlinear cb\nvia log10(z) inv 10**z"
unset log cb
set nonlinear cb via log10(z) inverse 10**z
set cbtics log
replot

unset multiplot

pause -1 "<cr> to continue"
reset
