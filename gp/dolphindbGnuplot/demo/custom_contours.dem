#
# New options for customizing contour lines
# set cntrparam {firstlinetype N} {{un}sorted}
#
set contours
set xrange [1:7.6]
set yrange [1:7.6]
set view 67., 180., 1.42, 1.05
set key outside right top
unset xtics
unset ytics
unset ztics

# Custom contour lines starting at 101
set linetype 101 lc "black"
set linetype 102 lc "black" lw 0.5 dt '.'
set linetype 103 lc "black" lw 0.5 dt '-'
set linetype 104 lc "black" lw 0.5
set linetype 105 lc 6

set multiplot layout 2,2 spacing 0,0

set key title "default"
splot x*y lw 2

set hidden3d offset -1
set key title "hidden3d"
splot x*y lw 2

set cntrparam first 101 sort
unset hidden3d
set key title "firstline sorted"
splot x*y lw 2

set hidden3d
set key title "hidden3d firstline sorted"
splot x*y lw 2
unset multiplot

pause -1 "<cr> to continue"
reset
